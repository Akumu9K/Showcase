// X = r1 * sin (p1-90) * cos (y1)
// Z = r1 * sin (p1-90) * sin (y1)
// Y = r1 * cos (p1-90) 
// (v - v * sqrt(|v|)) / |v| or (v * (1 - sqrt(|v|))) / |v|

// r1 is radius, p1 is pitch, y1 is yaw

// Generate r1 first.
Entropy Reflection
Numerical Reflection: 1
Numerical Reflection: 2
Division Distillation
Power Distillation
// Then p1
Entropy Reflection
Numerical Reflection: 2
Multiplicative Distillation
Numerical Reflection: 1
Subtractive Distillation
// Now the magic formula, the first one
Numerical Reflection: 4
Gemini Gambit
Length Purification
Numerical Reflection: 1
Numerical Reflection: 2
Division Distillation
Power Distillation
Multiplicative Distillation
Subtractive Distillation
Jester's Gambit
Length Purification
Division Distillation
// Now make it -180 - 0 (p1-90), in radians though
Numerical Reflection: 1
Subtractive Distillation
Arc's Reflection
Numerical Reflection: 2
Division Distillation
Multiplicative Distillation
// Calculate Y before X and Z
Dioscuri Gambit
Cosine Purification
Multiplicative Distillation
// Push it down
Rotation Gambit II
// Now y1
Entropy Reflection
Numerical Reflection: 1
Numerical Reflection: 2
Division Distillation
Multiplicative Distillation
Circle's Reflection
Multiplicative Distillation
// Stack now looks like: Y,r1,p1,y1
// Push y1 down
Rotation Gambit II
// Calculate the constant parts of X and Z, r1 * sin(p1-90), designated c1
Sine Purification
Multiplicative Distillation
// Duplicate the part and y1 for X and Z
Dioscuri Gambit
// Stack now looks like: Y,y1,c1,y1,c1
// First Z
Jester's Gambit
Sine Purification
Multiplicative Distillation
// Stack now looks like: Y,y1,c1,Z
// Push Z down
Rotation Gambit II
// Now finally, X
Jester's Gambit
Cosine Purification
Multiplicative Distillation
// Stack now looks like: Y,Z,X
// Push X down
Rotation Gambit II
// Stack now looks like: X,Y,Z
// Bring together
Vector Exaltation
